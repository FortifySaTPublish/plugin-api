buildscript {
  repositories {
        jcenter()
   }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

group = 'com.fortify.plugin'
version = '1.0.1'

allprojects {
	repositories {
		jcenter()
	}
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'osgi'
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
}

jar {
    manifest {
        name = 'Plugin API'
        instruction 'Bundle-Vendor', 'Fortify'
        instruction 'Bundle-Description', 'Fortify Plugin API'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives(sourcesJar) {
        type 'source'
    }
    archives(javadocJar) {
        type 'javadoc'
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        // disable the crazy super-strict doclint tool in Java 8
        //noinspection SpellCheckingInspection
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

publishing {
	publications {
		BintrayPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId group
			artifactId 'plugin-api'
			version version
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['BintrayPublication']
	pkg {
		repo = 'fortify-public'
		name = 'plugin-api'
		userOrg = user
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/fortify/plugin-api.git'
		labels = []
		publicDownloadNumbers = true
		version {
			name = version
			vcsTag = '1.0.1'
			desc = 'SSC plugin API release'
		}
	}
}
